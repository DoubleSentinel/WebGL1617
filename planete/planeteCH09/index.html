<!DOCTYPE html>
<html lang="fr">
    <head>
        <meta charset="utf-8" />
        <link rel="stylesheet" href="css/stylesheet.css">
		<!-- import of webgl utilities -->
        <script src="./js/commonFunctions.js"></script>
        <script src="./js/gl-matrix-min.js"></script>
        <script src="./js/tools.js"></script>
		<script src="debug/webgl-debug.js"></script>
		<!-- import application scripts -->
        
		<script src="./js/orbit.js"></script>
        <script src="./js/planet.js"></script>
		<script src="./js/skybox.js"></script>
		<script src="./js/skybox-loader.js"></script>
		<script src="./js/scene.js"></script>
		
		<!-- vertex shader for skybox -->
		<script type="x-shader/x-vertex" id="shader-vs-skybox">
			 uniform mat4 uPMatrix;
			 uniform mat4 uMVMatrix;
			 attribute vec3 aCoords;
			 varying vec3 vCoords;
			 void main() {
				vec4 eyeCoords = uMVMatrix * vec4(aCoords,1.0);
				vCoords = aCoords;
				gl_Position = uPMatrix * eyeCoords;
				
			 }
		</script>
		
		<!-- fragment shader fox skybox -->
		<script type="x-shader/x-fragment" id="shader-fs-skybox">
			 precision mediump float;
			 varying vec3 vCoords;
			 uniform samplerCube uSkybox1;
			 uniform samplerCube uSkybox2;
			 void main() {
				  vec4 color1 = textureCube(uSkybox1, vCoords);
				  vec4 color2 = textureCube(uSkybox2, vCoords);
				  gl_FragColor = mix(color1 , color2, 0.33); /// TODO : APPLY OTHER EFFECT HERE
			 }
		</script>
				
		
		
		<!-- vertex shader -->
        <script id="shader-vs" type="x-shader/x-vertex">
            const vec3 lightpos = vec3(0.0, 0.0, 5.0);
            const float PI = 3.14159;
            
            attribute vec3 aVertexPosition;
            attribute vec2 aTextureCoord;
            
            uniform mat4 uMVMatrix;
            uniform mat4 uPMatrix;
            uniform mat4 uNMatrix;
            
            uniform float radius;
            uniform float rotation;
            uniform float inclination;
			
            
			
            varying vec3 vLightpos;
            varying vec2 vTextureCoord;
            varying vec4 vPosition;
            varying mat3 tbn;
			
		            
            vec4 quat_from_axis_angle(vec3 axis, float angle)
            { 
              vec4 qr;
              float half_angle = (angle * 0.5) * PI / 180.0;
              qr.x = axis.x * sin(half_angle);
              qr.y = axis.y * sin(half_angle);
              qr.z = axis.z * sin(half_angle);
              qr.w = cos(half_angle);
              return qr;
            }
            vec4 quat_conj(vec4 q)
            { 
              return vec4(-q.x, -q.y, -q.z, q.w); 
            }
            vec4 quat_mult(vec4 q1, vec4 q2)
            { 
              vec4 qr;
              qr.x = (q1.w * q2.x) + (q1.x * q2.w) + (q1.y * q2.z) - (q1.z * q2.y);
              qr.y = (q1.w * q2.y) - (q1.x * q2.z) + (q1.y * q2.w) + (q1.z * q2.x);
              qr.z = (q1.w * q2.z) + (q1.x * q2.y) - (q1.y * q2.x) + (q1.z * q2.w);
              qr.w = (q1.w * q2.w) - (q1.x * q2.x) - (q1.y * q2.y) - (q1.z * q2.z);
              return qr;
            }
            vec3 rotate_vertex_position(vec3 position, vec3 axis, float angle)
            { 
              vec4 qr = quat_from_axis_angle(axis, angle);
              vec4 qr_conj = quat_conj(qr);
              vec4 q_pos = vec4(position.x, position.y, position.z, 0);
              
              vec4 q_tmp = quat_mult(qr, q_pos);
              qr = quat_mult(q_tmp, qr_conj);
              
              return vec3(qr.x, qr.y, qr.z);
            }
            void main(void)
            {
			
				
				vec3 vertex = rotate_vertex_position(aVertexPosition, vec3(0.0, 1.0, 0.0), rotation * 360.0);
				
				vec3 position = rotate_vertex_position(vertex * radius, vec3(1.0, 0.0, 0.0), 90.0 - inclination);
				
				vLightpos = (uMVMatrix * vec4(lightpos, 1.0)).xyz;
				
				vTextureCoord = aTextureCoord;
				
				vec3 normal = normalize((uNMatrix * vec4(position, 1.0)).xyz);
				float theta = acos(position.z / radius) + PI / 2.0;
				float phi = atan(position.y, position.x);
				vec3 tangent = normalize((uNMatrix * 
					vec4(
						sin(theta) * cos(phi) * radius,
						sin(theta) * sin(phi) * radius, 
						cos(theta) * radius, 
						1.0
					)).xyz);
				vec3 bitangent = normalize(cross(normal, tangent));
				tbn = mat3(tangent, bitangent, normal);
				
				vPosition = uMVMatrix * vec4(position, 1.0);
				
				gl_Position = uPMatrix * vPosition;
				
                
            }
        </script>
		<!-- fragment shader -->
        <script id="shader-fs" type="x-shader/x-fragment">
            #ifdef GL_ES
                precision highp float;
            #endif
            const float alpha = 32.0;
            
            uniform sampler2D uColorTexture;
            uniform sampler2D uNormalTexture;
            uniform sampler2D uSpecularTexture;
            uniform sampler2D uEarthNight;
            
            uniform bool uBoolNormal;
			uniform bool uBoolNight;
			
            uniform bool uDrawSkybox;
            varying vec3 vLightpos;
            varying vec2 vTextureCoord;
            varying vec4 vPosition;
            varying mat3 tbn;
			
            
            vec3 colorFromTextures(vec3 color, vec3 normal, vec3 specularMap, vec3 L)
            {
                vec3 N = normalize(tbn * normal);
                float lambertTerm = max(dot(N, L), 0.0);
                
                vec3 finalColor = vec3(1.0, 1.0, 1.0) * lambertTerm;
               
                vec3 E = normalize((-vPosition.xyz));
                vec3 R = reflect(-L, N);
                float specular = pow(max(dot(R, E), 0.0), alpha) * lambertTerm;
                finalColor += specular * specularMap;
                finalColor *= color;
                
                return finalColor;
            }
            void main(void)
            {    
				
				vec3 texelColor = texture2D(uColorTexture, vTextureCoord).rgb;
				vec3 color = vec3(0.0, 0.0, 0.0);
				
				vec3 L = normalize(vLightpos - vPosition.xyz);
				
				vec3 texelNormal = uBoolNormal ? texture2D(uNormalTexture, vTextureCoord).rgb * 2.0 - 1.0 : vec3(0.0, 0.0, 1.0);
				vec3 N = normalize(tbn * texelNormal);
				float lambertTerm = dot(N, L);
				if (lambertTerm > 0.0)
				{
					vec3 texelSpecular = texture2D(uSpecularTexture, vTextureCoord).rgb;
					color = colorFromTextures(texelColor, texelNormal, texelSpecular, L);
				}
				if (lambertTerm < 0.2)
				{
					vec3 texelNight = uBoolNight ? texture2D(uEarthNight, vTextureCoord).rgb : texture2D(uColorTexture, vTextureCoord).rgb *.5;
					float fade = lambertTerm > 0.0 ? 1.0 - lambertTerm * 5.0 : 1.0;
					color += texelNight * length(texelNight) * fade / 1.5;
				}
				
				gl_FragColor = vec4(color, 1.0);
				
				
            }
        </script>
    </head>
	<!-- initialize webgl onload -->
    <body onload="initWebGL()">
		<script>
			displayTitle("Ch.09 Projet fil rouge - Le ciel s'illumine", 1,1,1,1,1,1,1);
		</script>
		<br>
			Projet démo afin de faire un rendu de la terre et de la lune, étape 6. Mise en place d'une skybox avec un 2ème shader <a href="planet-ch09.zip">Télécharger la démo</a>
		<br>
		<!-- webgl canvas -->
		<canvas id="webgl-canvas" width="500" height="500">
		  HTML5 is not supported
		</canvas>
		<br/>
		<!-- subdivision slider -->
		Subdivisions : <input type="range" value="0" min="0" max="5" onchange="changeSubdivision(this)" />
		<br/>
    </body>
    <script src="./js/mouseMotionHandling.js"></script>
</html>