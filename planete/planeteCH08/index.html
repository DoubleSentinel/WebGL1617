<!DOCTYPE html>
<html lang="fr">
    <head>
        <meta charset="utf-8" />
        <link rel="stylesheet" href="css/stylesheet.css">
		<!-- import of webgl utilities -->
        <script src="./js/commonFunctions.js"></script>
        <script src="./js/gl-matrix-min.js"></script>
        <script src="./js/webglTools.js"></script>
		<script src="debug/webgl-debug.js"></script>
		<!-- import application scripts -->
        
		<script src="./js/orbit.js"></script>
        <script src="./js/planet.js"></script>
		<script src="./js/scene.js"></script>
		<!-- vertex shader -->
        <script id="shader-vs" type="x-shader/x-vertex">
            //Constant for light orientation & Pi
			const vec3 lightpos = vec3(0.0, 0.0, 5.0);
            const float PI = 3.14159;
            
            attribute vec3 aVertexPosition;
            attribute vec2 aTextureCoord;
            
            uniform mat4 uMVMatrix;
            uniform mat4 uPMatrix;
            uniform mat4 uNMatrix;
            
            uniform float radius;
            uniform float rotation;
            uniform float inclination;
			
			
            varying vec3 vLightpos;
            varying vec2 vTextureCoord;
            varying vec4 vPosition;
            varying mat3 tbn;
            /***********************************
			* Mathematical functions for quaternions
			************************************/
            vec4 quat_from_axis_angle(vec3 axis, float angle)
            { 
              vec4 qr;
              float half_angle = (angle * 0.5) * PI / 180.0;
              qr.x = axis.x * sin(half_angle);
              qr.y = axis.y * sin(half_angle);
              qr.z = axis.z * sin(half_angle);
              qr.w = cos(half_angle);
              return qr;
            }
            vec4 quat_conj(vec4 q)
            { 
              return vec4(-q.x, -q.y, -q.z, q.w); 
            }
            vec4 quat_mult(vec4 q1, vec4 q2)
            { 
              vec4 qr;
              qr.x = (q1.w * q2.x) + (q1.x * q2.w) + (q1.y * q2.z) - (q1.z * q2.y);
              qr.y = (q1.w * q2.y) - (q1.x * q2.z) + (q1.y * q2.w) + (q1.z * q2.x);
              qr.z = (q1.w * q2.z) + (q1.x * q2.y) - (q1.y * q2.x) + (q1.z * q2.w);
              qr.w = (q1.w * q2.w) - (q1.x * q2.x) - (q1.y * q2.y) - (q1.z * q2.z);
              return qr;
            }
            vec3 rotate_vertex_position(vec3 position, vec3 axis, float angle)
            { 
              vec4 qr = quat_from_axis_angle(axis, angle);
              vec4 qr_conj = quat_conj(qr);
              vec4 q_pos = vec4(position.x, position.y, position.z, 0);
              
              vec4 q_tmp = quat_mult(qr, q_pos);
              qr = quat_mult(q_tmp, qr_conj);
              
              return vec3(qr.x, qr.y, qr.z);
            }
			/***********************************
			* End of Mathematical functions for quaternions
			************************************/
			
            void main(void)
            {
			
				//Handle the planet rotation on itself on y axis, (*) Make it work !
				vec3 vertex = rotate_vertex_position(aVertexPosition, vec3(0.0, 1.0, 0.0), rotation * 360.0);
				
				//Handle the inclination of the earth on it's axis, (*) Make it work !
				vec3 position = rotate_vertex_position(vertex * radius, vec3(1.0, 0.0, 0.0), 90.0 - inclination);
				
				//Calculate the lights position
				vLightpos = (uMVMatrix * vec4(lightpos, 1.0)).xyz;
				
				//Retrive the texture coordinates for the point
				vTextureCoord = aTextureCoord;
				
				//Calculates the normal for the current point
				vec3 normal = normalize((uNMatrix * vec4(position, 1.0)).xyz);
				
				//Calculate the tangeant to the point
				float theta = acos(position.z / radius) + PI / 2.0;
				float phi = atan(position.y, position.x);
				vec3 tangent = normalize((uNMatrix * 
					vec4(
						sin(theta) * cos(phi) * radius,
						sin(theta) * sin(phi) * radius, 
						cos(theta) * radius, 
						1.0
					)).xyz);
				vec3 bitangent = normalize(cross(normal, tangent));
				tbn = mat3(tangent, bitangent, normal);
				
				//Calculate the position in space of the current point
				vPosition = uMVMatrix * vec4(position, 1.0);
				
				//Defines the projection of the point
				gl_Position = uPMatrix * vPosition;
				
                
            }
        </script>
		<!-- fragment shader -->
        <script id="shader-fs" type="x-shader/x-fragment">
            #ifdef GL_ES
                precision highp float;
            #endif
            const float alpha = 32.0;
            
            uniform sampler2D uColorTexture;
            uniform sampler2D uNormalTexture;
            uniform sampler2D uSpecularTexture;
            uniform sampler2D uEarthNight;
            
            uniform bool uBoolNormal;
			uniform bool uBoolNight;
			
            varying vec3 vLightpos;
            varying vec2 vTextureCoord;
            varying vec4 vPosition;
            varying mat3 tbn;
            //Function to retrive the color from a texture depending on the light, color and specular texture and the normals
            vec3 colorFromTextures(vec3 color, vec3 normal, vec3 specularMap, vec3 L)
            {
                vec3 N = normalize(tbn * normal);
                float lambertTerm = max(dot(N, L), 0.0);
                
                vec3 finalColor = vec3(1.0, 1.0, 1.0) * lambertTerm;
               
                vec3 E = normalize((-vPosition.xyz));
                vec3 R = reflect(-L, N);
                float specular = pow(max(dot(R, E), 0.0), alpha) * lambertTerm;
                finalColor += specular * specularMap;
                finalColor *= color;
                
                return finalColor;
            }
            void main(void)
            {    
				
					//Retrives the color of the current point based on the colorTexture
					vec3 texelColor = texture2D(uColorTexture, vTextureCoord).rgb;
					//Retrives a default color [black]
					vec3 color = vec3(0.0, 0.0, 0.0);
					
					//Normalize the light vector depending on the point
					vec3 L = normalize(vLightpos - vPosition.xyz);
					
					//Retrive the normal texel if it's enables, else takes a default [0.0,0.0,1.0]
					vec3 texelNormal = uBoolNormal ? texture2D(uNormalTexture, vTextureCoord).rgb * 2.0 - 1.0 : vec3(0.0, 0.0, 1.0);
					//Normalize the texel based on the tangeant result in the vertex shader
					vec3 N = normalize(tbn * texelNormal);
					//Calculate the lambertTerm between the light and the normal
					float lambertTerm = dot(N, L);
					//If the light is on the side where it's shone upon
					if (lambertTerm > 0.0)
					{
						//We retrive the specular map color
						vec3 texelSpecular = texture2D(uSpecularTexture, vTextureCoord).rgb;
						//We apply the color based on the texelColor, the texelNormal, the texelSpecular and the light
						color = colorFromTextures(texelColor, texelNormal, texelSpecular, L);
					}
					//If we are on the dark side of the planet
					if (lambertTerm < 0.2)
					{
						//We retrive the night texcel if it's enable, else we retrive the standard color texture 
						vec3 texelNight = uBoolNight ? texture2D(uEarthNight, vTextureCoord).rgb : texture2D(uColorTexture, vTextureCoord).rgb *.5;
						
						//Calculate a fade effect based on the intensity of the "darkness"
						float fade = lambertTerm > 0.0 ? 1.0 - lambertTerm * 5.0 : 1.0;
						//Applies the fade to the color
						color += texelNight * length(texelNight) * fade / 1.5;
					}
					//Defines the color of the fragment
					gl_FragColor = vec4(color, 1.0);
				

				
            }
        </script>
    </head>
	<!-- initialize webgl onload -->
    <body onload="initWebGL()">
		<script>
			displayTitle("Ch.08 Projet fil rouge - Notre première belle planète", 1,1,1,1,1,1,1);
		</script>
		<br>
			Projet démo afin de faire un rendu de la terre et de la lune, étape 5. Utilisation des subdivisions et des textures <a href="planet-ch08.zip">Télécharger la démo</a>
		<br>
		<!-- webgl canvas -->
		<canvas id="webgl-canvas" width="500" height="500">
		  HTML5 is not supported
		</canvas>
		<br/>
		<!-- subdivision slider -->
		Subdivisions : <input type="range" value="0" min="0" max="5" onchange="changeSubdivision(this)" />
		<br/>
		<!-- perspective/orthogonal projection button -->
		<button id="switchWireframe" onclick="changeProjection()">Perspective / Orthogonal</button>
		<br/>
		<!-- earth inclination slider -->
		Earth inclination [TODO] : <input type="range" value="0" min="0" max="360" onchange="todo(this)" />
		<br/>
		<!-- moon inclination slider -->
		Moon inclination [TODO] : <input type="range" value="0" min="0" max="360" onchange="todo(this)" />
		<br/>
		<!-- self rotation speed slider -->
		Planets self rotation speed [TODO] : <input type="range" value="0" min="0" max="50" onchange="todo(this)" />
		<br/>
		
    </body>
    <script src="./js/mouseMotionHandling.js"></script>
</html>